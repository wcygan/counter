plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.17'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    // Add Confluent repository
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

dependencies {
    implementation 'io.grpc:grpc-protobuf:1.61.0'
    implementation 'com.google.protobuf:protobuf-java:3.19.1'
    implementation 'com.google.guava:guava:32.1.1-jre'
    implementation 'org.apache.beam:beam-sdks-java-core:2.53.0'
    implementation 'org.apache.beam:beam-runners-flink-1.12:2.53.0'
    implementation 'org.apache.beam:beam-sdks-java-io-kafka:2.53.0'
    implementation 'org.apache.kafka:kafka-clients:2.8.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'org.testng:testng:7.5.1'
}

shadowJar {
    zip64 true
    archiveBaseName = 'beam-job'
    archiveClassifier = null
    archiveVersion = '1.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.61.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir '../../generated/java' // path to your generated files
        }
    }
    test {
        java {
            srcDir 'src/test/java' // test source directory
        }
        resources {
            srcDir 'src/test/resources' // test resources directory, if you have any
        }
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'io.counter.beam.App'
}

tasks.named('test') {
    // Use TestNG for unit tests.
    useTestNG()
}
